# Visualisation of coarse-grained particles (Example 3 in published paper)

# Create benzene model
int n;
newModel("benzene");
for (n=1; n<7; ++n)
{
	move(0, 1.39, 0);
	newAtom(C);
	rotZ(-60.0);
	move(0, 1.0, 0);
	newAtom(H);
	move(0, -1.0, 0);
	rotZ(120);
}
rebond();
selectAll();
copy();
setupComponent("number", 1, 200);

# Create fluorobenzene model
newModel("fluorobenzene");
paste();
selectNone();
select(H);
transmute(F);
setupComponent("number", 1, 200);

# Create naphthalene model
newModel("naphthalene");
for (n=1; n<11; ++n)
{
	chain(C, n%2+1);
	if (n%5 == 0) rotZ(-60.0);
	else rotZ(60.0);
	move(0, 1.39, 0);
}
newBond(1, 10, "double");
newBond(5, 10);
addHydrogen();
setupComponent("number", 1, 100);

# Create mixture model
newModel("mixture");
cell(100, 60, 25, 90, 90, 90);
disorder("None");

# Fold molecules into cell and add ellipsoids
foldMolecules();
selectPattern(1);
aten.prefs.glyphColour = {1.0, 1.0, 1.0, 1.0};
autoEllipsoids();
selectPattern(2);
aten.prefs.glyphColour = {0.2, 1.0, 0.2, 1.0};
autoEllipsoids();
selectPattern(3);
aten.prefs.glyphColour = {0.0, 0.0, 1.0, 1.0};
autoEllipsoids();

# Hide original atoms
selectAll();
hide();
